import qs
import qs.modules.common
import qs.modules.common.widgets
import qs.services
import QtQuick
import QtQuick.Layouts

StyledPopup {
    id: root

    ColumnLayout {
        id: columnLayout
        anchors.centerIn: parent
        spacing: 4

        StyledPopupHeaderRow {
            icon: "devices/battery-symbolic"
            label: `${Math.round(Battery.percentage * 100)}%`
        }

        StyledPopupValueRow {
            visible: {
                let timeValue = Battery.isCharging ? Battery.timeToFull : Battery.timeToEmpty;
                let power = Battery.energyRate;
                return Battery.available && !(Battery.chargeState == 4 || timeValue <= 0 || power <= 0.01);
            }
            icon: "actions/appointment-new-symbolic"
            label: Battery.isCharging ? Translation.tr("Time to full:") : Translation.tr("Time to empty:")
            value: {
                function formatTime(seconds) {
                    var h = Math.floor(seconds / 3600);
                    var m = Math.floor((seconds % 3600) / 60);
                    if (h > 0)
                        return `${h}h ${m}m`;
                    else
                        return `${m}m`;
                }
                if (Battery.isCharging)
                    return formatTime(Battery.timeToFull);
                else
                    return formatTime(Battery.timeToEmpty);
            }
        }

        StyledPopupValueRow {
            visible: Battery.available && Battery.chargeState != 4 && Battery.energyRate > 0.01
            icon: "status/plugged-into-power-symbolic"
            label: {
                if (Battery.chargeState == 4) {
                    return Translation.tr("Fully charged");
                } else if (Battery.chargeState == 1) {
                    return Translation.tr("Charging:");
                } else {
                    return Translation.tr("Discharging:");
                }
            }
            value: {
                if (Battery.chargeState == 4) {
                    return "";
                } else {
                    return `${Battery.energyRate.toFixed(1)}W`;
                }
            }
        }

        StyledPopupValueRow {
            icon: "status/weather-windy-symbolic"
            label: Translation.tr("Power mode:")
            value: {
                switch(PowerProfiles.profile) {
                    case PowerProfile.PowerSaver: return Translation.tr("Power Saver");
                    case PowerProfile.Balanced: return Translation.tr("Balanced");
                    case PowerProfile.Performance: return Translation.tr("Performance");
                    default: return Translation.tr("Balanced");
                }
            }
        }
    }
}
